#include "Plasma.nddl"
#include "PlannerConfig.nddl"

enum Location {Hill, Rock, Lander}

// Declare a class Rover such that it does not imply a mutex by extending a timeline.
class Rover {
 predicate At{
  Location location;
 }

 predicate Going{
  Location from;
  Location to;
 }

 UnaryResource mutex; // Embed a mutex as a Unary Resource

 // Constructor allocates embedded mutex
 Rover(){
  mutex = new UnaryResource();
 }
}

Rover::At{
 // Here are the usual rules we would expect to see
 meets (object.Going to);
 eq(to.from, location);
 met_by (object.Going from);
 eq(from.to, location);

 // Allocate unary resource transaction to produce at the end and
 // consume at the start.
 equals(UnaryResource.uses);
}

Rover::Going{
 // Usual rules you expect to see
 neq(from, to);
 meets(object.At _to);
 eq(to, _to.location);
 met_by(object.At _from);
 eq(_from.location, from);

 // Allocate unary resource transaction to produce at the start and
 // consume at the start.
 equals(UnaryResource.uses);
}

// Set up the objects in the system
PlannerConfig world = new PlannerConfig(0, 100, 50);
Rover rover = new Rover();
close();

// First position
goal(Rover.At g0);
g0.start.specify(0);
g0.location.specify(Hill);
g0.activate();

// Second position
goal(Rover.At g1);
g1.start.specify(99);
g1.location.specify(Lander);
g1.activate();

